buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
    }
}

allprojects {
    group = 'com.datatrees.spider'
    version = '4.2.0'
}

ext {
    env = System.getProperty("env", "dev")
}

subprojects {

    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
        }
    }

    configurations {
        provided
        compile.extendsFrom provided
        deployerJars

//        all*.exclude group: "org.springframework", module: "spring"
        all*.exclude group: "log4j", module: "log4j"
        all*.exclude group: "org.slf4j", module: "slf4j-log4j12"
        all*.exclude group: "com.datatrees.rawdata", module: "AbstractPlugin"
        all*.exclude group: "org.jfrog.jade.plugins.common", module: "*"
        all*.exclude group: "org.apache.ibatis", module: "ibatis-sqlmap"
        all*.exclude group: "org.springframework", module: "spring-ibatis"
        all*.exclude group: "com.datatrees.crawler", module: "crawler-core"
        all*.exclude group: "io.netty", module: "netty-all"
    }

    dependencies {
        testCompile ("junit:junit")
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        mavenLocal()
        maven { url NEXUS_REPO_URL }
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, "seconds"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                configuration = configurations.deployerJars
                snapshotRepository(url: NEXUS_REPO_SNAPSHOT_URL) {
                    authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
                }
                repository(url: NEXUS_REPO_RELEASE_URL) {
                    authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
                }
            }

        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }
            }
        }
    }
}


